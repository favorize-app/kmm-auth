plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'org.jetbrains.dokka' version "1.7.10"
    id 'maven-publish'
}

apply from: '../properties.gradle'

android {
    namespace setup.appId
    compileSdkVersion setup.compileSdkVersion

    defaultConfig {
        minSdkVersion setup.minSdkVersion
        targetSdkVersion setup.targetSdkVersion
        versionCode buildVersionCode()
        versionName buildVersionName()

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        multiDexEnabled true

        //routes
        resValue "string", "route_signin", String.format("%s%s", deeplink, "/sign-in")
        resValue "string", "route_otp", String.format("%s%s", deeplink, "/otp/{state}/{country}/{phone}/{duration}/{transactionId}")
        resValue "string", "route_register", String.format("%s%s", deeplink, "/register/{country}/{phone}/{transactionId}")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "GOOGLE_WEB_CLIENT_ID", String.format("%s", google.webClientId.store)
            resValue "string", "FB_APP_ID", String.format("%s", fb.appId.store)
            resValue "string", "FB_LOGIN_PROTOCOL_SCHEME", String.format("fb%s", fb.appId.store)
        }
        debug {
            testCoverageEnabled = true
            resValue "string", "GOOGLE_WEB_CLIENT_ID", String.format("%s", google.webClientId.dev)
            resValue "string", "FB_APP_ID", String.format("%s", fb.appId.dev)
            resValue "string", "FB_LOGIN_PROTOCOL_SCHEME", String.format("fb%s", fb.appId.dev)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding = true
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    task installLocalGitHook(type: Copy) {
        from new File(rootProject.rootDir, 'scripts/pre-commit')
        into { new File(rootProject.rootDir, '.git/hooks') }
        fileMode 0775
    }

    build.dependsOn installLocalGitHook
}

dokkaGfm {
    suppressInheritedMembers.set(true)
    outputDirectory.set(file("${rootProject.rootDir}/docs/${project.name}"))
}

dependencies {
    implementation project(":shared")
    dep.each {
        if (it.configuration == "platform") {
            add("implementation", platform(it.dependency))
        } else {
            add(it.configuration, it.dependency, it.options)
        }
    }
}

afterEvaluate {
    def buildTypes = android.buildTypes.collect { type -> type.name }
    publishing {
        publications {
            buildTypes.each { buildTypeName ->
                "$buildTypeName"(MavenPublication) {
                    from components."$buildTypeName"
                    groupId "${setup.appId}"
                    artifactId "$buildTypeName"
                    version android.defaultConfig.versionName
                }
            }
        }

        repositories {
            maven {
                name = "Auth"
                url = uri("https://gitlab.com/api/v4/projects/38961532/packages/maven")
                credentials(HttpHeaderCredentials) {
                    name = 'Private-Token'
                    value = gitlab.publishToken
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
    }
}